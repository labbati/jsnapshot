group 'com.labbati'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.researchgate.release'

sourceCompatibility = 1.8

bintray {
    if (project.hasProperty("bintray_user")) {
        // From gradle.properties
        user = bintray_user
        key = bintray_key
    }
    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = 'labbati-maven'
        name = 'jsnapshot'
        userOrg = 'labbati'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/labbati/jsnapshot.git'
        githubRepo = 'labbati/jsnapshot'
        version {
            name = version.name
            desc = 'jsnapshot ' + version.name + ' final'
            released  = new Date()
        }
    }
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = ''
    preTagCommitMessage = 'New version of jsnapshot released: '
    tagCommitMessage = 'New version of jsnapshot released: '
    newVersionCommitMessage = 'Preparing for version: '
    tagTemplate = '${version}'
    buildTasks = ['build', 'createPom', 'replaceVersionInREADME', 'bintrayUpload']

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

task createPom << {
    pom {
        project {
            inceptionYear '2018'
            licenses {
                license {
                    name 'MIT'
                }
            }
        }
    }.writeTo("pom.xml")
}

task replaceVersionInREADME << {
    ant.replaceregexp(match:'<version>([0-9\\.]+)</version>', replace:"<version>${version}</version>", flags:'g', byline:true) {
        fileset(dir: '.', includes: 'README.md')
    }
    ant.replaceregexp(match:'com\\.labbati\\:jsnapshot\\:([0-9\\.]+)', replace:"com.labbati:jsnapshot:${version}", flags:'g', byline:true) {
        fileset(dir: '.', includes: 'README.md')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.9.0"
}
